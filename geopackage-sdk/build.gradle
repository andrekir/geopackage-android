apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "mil.nga.geopackage"
archivesBaseName = "geopackage-android"
version = "3.3.0"
def remotePublish = false

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        testApplicationId 'mil.nga.geopackage.test'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    configurations {
        javadocDeps
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }
    }

    task javadoc(type: Javadoc) {
        title = "$archivesBaseName $version API"
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.javadocDeps
        classpath += files("${android.sdkDirectory}/extras/android/support/v13/android-support-v13.jar")
        options.links("http://ngageoint.github.io/geopackage-core-java/docs/api/")
        options.links("http://ngageoint.github.io/simple-features-wkb-java/docs/api/")
        options.links("http://ngageoint.github.io/simple-features-geojson-java/docs/api/")
        options.links("http://ngageoint.github.io/simple-features-proj-java/docs/api/")
        options.links("http://ngageoint.github.io/simple-features-java/docs/api/")
        options.links("http://ngageoint.github.io/tiff-java/docs/api/")
        options.links("http://ormlite.com/javadoc/ormlite-core/")
        options.links("http://ormlite.com/javadoc/ormlite-android/")
        options.links("http://docs.oracle.com/javase/7/docs/api/")
        options.links("http://hjg.com.ar/pngj/apidocs/")
        options.linksOffline("http://d.android.com/reference","${android.sdkDirectory}/docs/reference")
        destinationDir = file("../javadoc/")
        failOnError false
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java.srcDirs
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required {remotePublish}
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {

                if (!remotePublish) {

                    repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository')

                } else {

                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'GeoPackage Android'
                        packaging 'aar'
                        description 'GeoPackage Android implementation'
                        url 'https://github.com/ngageoint/geopackage-android'

                        scm {
                            url 'git@github.com:ngageoint/geopackage-android.git'
                            connection 'scm:git:git@github.com:ngageoint/geopackage-android.git'
                            developerConnection 'scm:git:git@github.com:ngageoint/geopackage-android.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'https://github.com/ngageoint/geopackage-android/blob/master/LICENSE.txt'
                                distribution 'repo'
                            }
                        }

                        organization {
                            name 'National Geospatial-Intelligence Agency'
                            url 'https://www.nga.mil'
                        }

                        developers {
                            developer {
                                id 'bosborn'
                                name 'Brian Osborn'
                                email 'osbornb@bit-sys.com'
                                organizationUrl 'https://www.bit-sys.com'
                                roles {
                                    role 'developer'
                                }
                                timezone 'UTC-07'
                            }
                        }
                    }
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

}

dependencies {
    api 'androidx.appcompat:appcompat:1.0.2'
    api 'com.j256.ormlite:ormlite-android:5.1'
    api 'mil.nga.geopackage:geopackage-core:3.3.0'
    api 'ar.com.hjg:pngj:2.1.0'
    api 'mil.nga:tiff:2.0.1'
    api 'mil.nga:sqlite-android:3270200'
    javadocDeps 'com.j256.ormlite:ormlite-android:5.1',
            'mil.nga.geopackage:geopackage-core:3.3.0',
            'ar.com.hjg:pngj:2.1.0',
            'mil.nga:tiff:2.0.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}
